#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use English qw(-no_match_vars);
use Getopt::Long;
use Pod::Usage;

use FusionInventory::Agent;
use FusionInventory::Agent::Config;
use FusionInventory::Agent::Logger;
use FusionInventory::Agent::Target;
use FusionInventory::Agent::Task::NetInventory;
use FusionInventory::Agent::Tools;

my %types = (
    1 => 'COMPUTER',
    2 => 'NETWORKING',
    3 => 'PRINTER',
    4 => 'STORAGE',
    5 => 'POWER',
    6 => 'PHONE',
    7 => 'VIDEO',
);

my %setup = (
    confdir => './etc',
    datadir => './',
    vardir  => './var',
);

my $options = {
    'workers'   => 0,
    'version'   => '2c',
    'community' => 'public',
    'conf-file' => $setup{confdir} . '/agent.cfg',
};

GetOptions(
    $options,
    'config=s',
    'conf-file=s',
    'target=s',
    'type=s',
    'community=s',
    'version=s',
    'username=s',
    'authpassword=s',
    'authprotocol=s',
    'privpassword=s',
    'privprotocol=s',
    'entity=s',
    'logger-verbosity=s',
    'workers=i',
    'timeout=i',
    'help',
    # deprecated
    'debug+',
    'host=s',
    'file=s',
) or pod2usage(-verbose => 0);

pod2usage(-verbose => 0, -exitval => 0) if $options->{help};

pod2usage(
    -message => "--host option is deprecated, use regular argument instead\n",
    -verbose => 0
) if $options->{host};

pod2usage(
    -message => "--file option is deprecated, use regular argument instead\n",
    -verbose => 0
) if $options->{file};

pod2usage(
    -message => "no host given, aborting\n", -verbose => 0
) unless @ARGV;

pod2usage(
    -message => "invalid type '$options->{type}', aborting\n",
    -verbose => 0
) if $options->{type} && none { $options->{type} eq $_ } values %types;

my $host_id = 1;
my @jobs;
foreach my $arg (@ARGV) {
    my $job;
    foreach my $parameter (split(',', $arg)) {
        my ($key, $value) = split(':', $parameter);
        if (!$value) {
            $job->{host} = $key;
        } else {
            $job->{$key} = $value;
        }
    }
    $job->{id}           ||= $host_id++;
    $job->{entity}       ||= $options->{entity};
    $job->{type}         ||= $options->{type};
    $job->{version}      ||= $options->{version};
    $job->{community}    ||= $options->{community};
    $job->{authpassword} ||= $options->{authpassword};
    $job->{authprotocol} ||= $options->{authprotocol};
    $job->{privpassword} ||= $options->{privpassword};
    $job->{privprotocol} ||= $options->{privprotocol};
    push @jobs, $job;
}

my $config = FusionInventory::Agent::Config->create(
    backend => $options->{'config'},
    file    => $options->{'conf-file'},
    options => {
        logger => {
            verbosity => $options->{'logger-verbosity'}
        },
    }
);

my $logger = FusionInventory::Agent::Logger->create(
    backend   => $config->{logger}->{backend},
    file      => $config->{logger}->{file},
    maxsize   => $config->{logger}->{maxsize},
    facility  => $config->{logger}->{facility},
    verbosity => $config->{logger}->{verbosity},
);

my $agent = FusionInventory::Agent->new(
    config => $config,
    logger => $logger,
    setup  => \%setup
);
$agent->init();
$agent->initHandlers();

my $task = FusionInventory::Agent::Task::NetInventory->new(
    logger => $logger,
    config => $config,
);

$task->configure(
    pid     => 1,
    workers => $options->{workers},
    timeout => $options->{timeout},
    jobs    => \@jobs,
);

my $target = FusionInventory::Agent::Target->create(
    spec   => $options->{target},
    logger => $logger,
    config => $config,
);

$agent->executeTask(task => $task, target => $target);

__END__

=head1 NAME

fusioninventory-netinventory - Standalone network inventory

=head1 SYNOPSIS

B<fusioninventory-netinventory>
S<[B<--config> I<string>]>
S<[B<--conf-file> I<path>]>
S<[B<--target> I<string>]>
S<[B<--version> I<string>]>
S<[B<--community> I<string>]>
S<[B<--username> I<string>]>
S<[B<--authpassword> I<string>]> S<[B<--authprotocol> I<string>]>
S<[B<--privpassword> I<string>]> S<[B<--privprotocol> I<string>]>
S<[B<--timeout> I<integer>]>
S<[B<--workers> I<integer>]> S<[B<--entity> I<string>]>
S<[B<--logger-verbosity> I<string>]>
I<host> S<[I<host> ...]>

=head1 DESCRIPTION

F<fusioninventory-netinventory> allows to run a network inventory task without
a GLPI server.

=head1 OPTIONS

=over

=item B<--config> I<string>

Configuration backend to use (default: file).

The available backends are:

=over 4

=item

file: read configuration from a file

=item

registry: read configuration from the registry

=item

none: don't read any configuration

=back

=item B<--conf-file> I<path>

configuration file path (with file backend)

=item B<--target> I<string>

target path (for directory) or url (for server)

=item B<--version> I<string>

SNMP version (default: 2c)

=item B<--community> I<string>

SNMP v1/v2c community (default: public)

=item B<--username> I<string>

SNMP v3 username

=item B<--authprotocol> I<string>

SNMP v3 authentication protocol

=item B<--authpassword> I<string>

SNMP v3 authentication password

=item B<--privprotocol> I<string>

SNMP v3 privacy protocol

=item B<--privpassword> I<string>

SNMP v3 privacy password

=item B<--timeout> I<integer>

SNMP timeout (default: 15s)

=item B<--workers> I<integer>

worker processes number (default: 0)

=item B<--entity> I<string>

GLPI entity

=item B<--logger-verbosity> I<string>

Set B<logger/verbosity> option, see L<agent.cfg> for details.

=back

=head1 EXAMPLES

Execute an inventory of a single remote host, sending the results to stdout:

    $> fusioninventory-netinventory 192.168.1.24

Execute an inventory of a single remote host, sending the results to a directory:

    $> fusioninventory-netinventory --directory /tmp 192.168.1.24

Execute an inventory of a single remote host, sending the results to a server:

    $> fusioninventory-netinventory \
        --server http://glpi.domain.com/plugins/fusioninventory/ \
        192.168.1.24

Execute an inventory of two remote hosts, sending the results to stdout:

    $> fusioninventory-netinventory 192.168.1.24 192.168.1.25

Execute an inventory of two remote hosts, using same SNMP credentials,
sending the results to stdout:

    $> fusioninventory-netinventory --version 1 --community foo \
        192.168.1.24 192.168.1.25

Execute an inventory of two remote hosts, using different SNMP credentials,
sending the results to stdout:

    $> fusioninventory-netinventory \
        host:192.168.1.24,version:1,community:foo \
        host:192.168.1.25,version:2c,community:bar

Execute an inventory of a single host, identified as a printer, sending the
results to stdout:

    $> fusioninventory-netinventory host:192.168.1.24,type:printer

Execute an inventory of a single host, from a snmpwalk capture, sending the
results to stdout:

    $> fusioninventory-netinventory file:192.168.1.24.walk
